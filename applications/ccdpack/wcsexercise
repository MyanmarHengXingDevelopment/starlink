#!/bin/csh
#+
#  Name:
#     WCSEXERCISE

#  Purpose:
#     Tests WCS-aware aspects of CCDPACK functionality.

#  Language:
#     C shell

#  Description:
#     The script creates a series of test frames using CCDGENERATE. It
#     then executes various of the CCDPACK Atasks simulating a
#     registration sequence. Intermediary results are displayed 
#     (if requested).

#  Copyright:
#     Copyright (C) 1992-1993 Science & Engineering Research Council.
#     Copyright (C) 1995, 1997-1999, 2001 Central Laboratory of the
#     Research Councils. All Rights Reserved.

#  Licence:
#     This program is free software; you can redistribute it and/or
#     modify it under the terms of the GNU General Public License as
#     published by the Free Software Foundation; either version 2 of
#     the License, or (at your option) any later version.
#
#     This program is distributed in the hope that it will be
#     useful, but WITHOUT ANY WARRANTY; without even the implied
#     warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
#     PURPOSE. See the GNU General Public License for more details.
#
#     You should have received a copy of the GNU General Public License
#     along with this program; if not, write to the Free Software
#     Foundation, Inc., 51 Franklin Street,Fifth Floor, Boston, MA
#     02110-1301, USA

#  Authors:
#     PDRAPER: Peter Draper (STARLINK)
#     MBT: Mark Taylor (STARLINK)
#     {enter_new_authors_here}

#  History:
#     4-APR-1992 (PDRAPER):
#        Original version.
#     29-JUN-1993 (PDRAPER):
#              Changed to include automated alignment.
#     18-JUL-1995 (PDRAPER):
#        Now references $CCDPACK_DIR instead of editting INSTALL at
#        install time.
#     21-MAR-1997 (PDRAPER):
#        Now does above properly.
#     20-JAN-1998 (PDRAPER):
#        Fixed problem with device=! not being accepted on OSF1.
#     17-FEB-1999 (MBT):
#        Modified for use with WCS-aware CCDPACK tasks.
#     26-APR-1999 (MBT):
#        Changed name CCDEXERCISE -> WCSEXERCISE and removed
#        pre-registration steps.
#     9-JAN-2001 (MBT):
#        Added demo of DRAWNDF task.
#     {enter_further_changes_here}

#  Bugs:
#     {note_any_bugs_here}

#-
#
      onintr EXIT
      set null = "\!"
      set abort = "\!\!"

#  Initialise CCDPACK within this shell.
      if ( $?CCDPACK_DIR ) then 
         if ( -e $CCDPACK_DIR/ccdpack.csh ) then 
            source $CCDPACK_DIR/ccdpack.csh
         else 
            echo "Sorry cannot run the demonstration script as the CCDPACK"
            echo "initialisation file CCDPACK_DIR/ccdpack.csh does not seem to"
            echo "exist. This probably means that you are running an incompatible"
            echo "version of CCDPACK or your installation is broken."
            exit 1
         endif
      else 
        echo "Sorry cannot run the demonstration script as the CCDPACK_DIR"
        echo "environment variable is not set. This is normally set by"
        echo "sourcing the Starlink login file (usually in your .login file)."
        exit 1
      endif

#  Establish the current directory as ADAM_USER and clear 
#  any global values already existing 
      setenv ADAM_USER $cwd
      if ( -e ${ADAM_USER}/GLOBAL.sdf ) then
	 ccdclear accept 
      endif

#  Does the user want to see image display activity?
      echo -n "DISPLAY - Display device (xw for xwindows; ! for none) > "
      set device = $<
      if ( "$device" == "$abort" || "$device" == "" ) then
         goto EXIT
      else
         if ( "$device" == "$null" ) then
            set device = "NONE"
         endif
      endif

#  Set a file extension for generating data.
      if ( $?CCDPACK_EXT ) then 
         set exten =  $CCDPACK_EXT
      else 
         set exten = ".sdf"
      endif

#  If we're going to use a device we need KAPPA.
      if ( "$device" != "NONE" ) then
         source $KAPPA_DIR/kappa.csh
         gdset $device \\
         gdclear \\
         paldef \\
      endif

#  Try to locate the object specification file. If this does not exists
#  then exit.
      set objfile = ccdtest2_obj.dat
      set astfile = ccdtest.ast
      foreach file ( $objfile $astfile )
         if ( -e $file ) then
	    'rm' $file
         endif
         if ( -e $CCDPACK_DIR/$file ) then
	    cp $CCDPACK_DIR/$file ./
         else
	    echo "Cannot locate file $CCDPACK_DIR/$file"
	    echo "- test terminated."
	    exit
         endif
      end

#  Create the test frames
      echo " "
      echo "  Creating the test data. "
      echo " "
      echo "  The test data consists of :-"
      echo "     6 object fields. "
      echo " "
      echo "  The target data is a simulated starfield in which two exposures"
      echo "  have been made with the same 3-chip mosaic camera.  The camera"
      echo "  has been rotated by 180 degrees between exposures."
      echo " "
      echo "  Generate the first exposure."
      ccdgenerate \
         nseq=3 \
         file=$objfile \
         type=$exten \
         pixels='[64,128]' \
         origins='[-1,-101,-69,-101,297,-1]' \
         angles='[0,0,270]' \
         reduced \
         reset accept
#     end ccdgenerate
      mv data1$exten data-1a$exten
      mv data2$exten data-1b$exten
      mv data3$exten data-1c$exten
      set ndfs = ( data-1a data-1b data-1c )

      echo " "
      echo "  Generate the second exposure."
      ccdgenerate \
         nseq=3 \
         file=$objfile \
         type=$exten \
         pixels='[64,128]' \
         origins='[163,210,95,210,-15,163]' \
         angles='[180,180,90]' \
         reduced \
         reset accept
#     end ccdgenerate
      mv data1$exten data-2a$exten
      mv data2$exten data-2b$exten
      mv data3$exten data-2c$exten
      set ndfs = ( $ndfs data-2a data-2b data-2c )

#  Get the AST file describing the relative positions of images on the 
#  focal plane of the notional instrument.
      if ( -e $astfile ) then
         'rm' $astfile
      endif
      if ( -e $CCDPACK_DIR/$astfile ) then
         cp $CCDPACK_DIR/$astfile ./
      endif

      echo " "
      echo "  We use an AST file containing information about the relative"
      echo "  positions of the CCDs on the focal plane of the mosaic camera."
      echo "  The application ASTIMP imports this information from the AST"
      echo "  file to the WCS components of the images.  If the telescope"
      echo "  adds approximate pointing information to the images which can"
      echo "  be used for provisional alignment then this step may not be"
      echo "  necessary.  We will use this as an aid to registration, but where"
      echo "  possible alignment will be done by object matching."
      echo " "
      echo "  While importing the alignment information, we use the knowledge"
      echo "  the camera was rotated by 180 degrees to import the second set"
      echo "  of data rotated."
      echo " "
      echo "  The domain of each set of observations is assigned a different"
      echo "  name since the camera was moved, so that the coordinates of"
      echo "  first observation are not the same as those of the second."
      echo " "
      astimp \
         in='data-1?' \
         astfile=$astfile \
         indomain=set1 \
         reset accept
#     end astimp
      astimp \
         in='data-2?' \
         astfile=$astfile \
         indomain=set2 \
         rot=180 \
         reset accept
#     end astimp

#  If display capability is enabled then display the data frames.
      if ( "$device" != "NONE" ) then
         echo " "
         echo "  Displaying pre-reduced target frames."
         echo " "
         picdef \
            mode=array \
            xpic=3 \
            ypic=2 \
            prefix=a \
            accept
#        end picdef
         lutheat \
            accept
#        end lutheat
         foreach num ( 1 2 3 4 5 6 )
            picsel label=a$num accept
            display \
               in=$ndfs[$num] \
               mode=percentiles \
               percentiles='[2,98]' \
               style="'colour(numlab)=red'" \
               accept
#           end display
         end
      endif

#  Add note to logfile.
      echo " "
      echo "  Adding a note to the logfile. "
      echo " "
      set name = `who am i`
      ccdnote \
         '"'$name : exercising WCS registration in CCDPACK'"' \
         accept
#     end ccdnote

#  Now proceed to test out some alignment functionality.
      echo " "
      echo "  CCDPACK will now attempt to realign the target frames to"
      echo "  produce a complete mosaic of whole of the target region."
      echo " "

#  Locate all the objects on the frames
      echo " " 
      echo "  The first stage of the automated registration process is"
      echo "  to detect the positions of objects (stars and galaxies)."
      echo "  "
      findobj \
         in='data-??' \
	 outlist='*.find' \
         percentile=94 \
	 reset accept
#     end findobj
      if ( "$device" != "NONE" ) then

#  Display the objects located.
         echo " "
         echo "  Displaying the positions of the objects which have been"
         echo "  detected."
         echo " "
         foreach num ( 1 2 3 4 5 6 )
            picsel label=a$num accept
            plotlist \
               inlist=$ndfs[$num] \
               palnum=4 \
               mtype=23 \
               logto=neither \
               accept
#           end plotlist
         end
      endif
	
#  Determine matches between the positions.
      echo " "
      echo "  After locating the objects it is now necessary to determine"
      echo "  which objects correspond.  Note that because of the alignment"
      echo "  of the two exposures some of the images have no matching objects."
      echo " "
      findoff \
         inlist='data-??' \
	 ndfnames=true \
         usewcs=true \
         restrict=false \
         override=true \
	 outlist='*.off' \
	 reset accept
#     end findoff
      if ( "$device" != "NONE" ) then

#  Display the objects located.
         echo " "
         echo "  Displaying the labels of objects which have been matched."
         echo " "
         foreach num ( 1 2 3 4 5 6 )
            picsel label=a$num accept
            plotlist \
               inlist=$ndfs[$num] \
               palnum=3 \
               mtype=-1 \
               thick=2 \
               msize=1.5 \
               logto=neither \
               accept
#           end plotlist
         end
      endif

#  Set the registration structures.
      echo " "
      echo "  Now that the object-object correspondence is known it is"
      echo "  possible to work out the inter-NDF transformations."
      echo "  The next routine, REGISTER, selects images which have matched"
      echo "  position lists and adds a new frame to the WCS component with" 
      echo "  a domain name of CCD_REG for all those which can be mutually"
      echo "  aligned by object matching."
      echo " "
      rm -f ccdexercise.ast
      register \
         inlist='data-??' \
	 fittype=2 \
	 reset accept
#     end register

      echo " "
      echo "  Not all of the images could be registered by object matching, so"
      echo "  there is no single domain in which they can all be aligned."
      echo "  However, by using the information in the WCS component it is"
      echo "  possible to find an aligment of all of them.  The task WCSREG"
      echo "  makes the best alignment it can; in this case it aligns using"
      echo "  the CCD_REG frame (inserted by the object matching) if it "
      echo "  exists, and if not, uses the SET1 or SET2 frames, which describe"
      echo "  what is known about the geometry of the camera."
      echo " "
      wcsreg \
         in='data-??' \
         domains='[ccd_reg,set1,set2]' \
         reset accept
#     end wcsreg

      echo " "
      echo "  Write the alignment we have found to an exported AST file."
      echo "  This contains information about the relative positions of"
      echo "  the CCDs in the mosaic camera, and could be used to process"
      echo "  future data sets."
      astexp \
         in='data-1?' \
         astfile=ccdtest_out.ast \
         idtype=fitsid \
         fitsid=ISEQ \
         outdomain=TEST_DATA \
         reset accept
#     end astexp

#  Plot the outlines.
      if ( "$device" != "NONE" ) then
         echo " "
         echo "  Registration is complete: display the alignment of the"
         echo "  images on the graphics device prior to generating the"
         echo "  mosaic.  Also plot the points whose matching caused the"
         echo "  alignment."
         echo " "
         gdclear \\
         drawndf \
            in='data-??' \
            clear \
            reset accept
#        end drawndf
         plotlist \
            inlist='data-1?' \
            mtype=2 \
            palnum=2 \
            reset accept
#        end plotlist
         plotlist \
            inlist='data-2?' \
            mtype=4 \
            palnum=3 \
            reset accept
#        end plotlist
      endif
      echo " "

#  Resample the data.
      echo " "
      echo "  The reduced NDFs will now be resampled to the same coordinate"
      echo "  system."
      echo " "
      tranndf \
         in='data-??' \
         out='*-r' \
         reset accept
#     end tranndf

#  Combine to a mosaic.
      echo " "
      echo "  The aligned images are now combined into a single mosaic."
      echo " "
      makemos \
         in='data-??-r' \
	 scale=false \
	 zero=false \
	 out=mosaic \
	 reset accept
#     end makemos

#  We also do the combination using the drizzling algorithm.  This is 
#  not necessary, or particularly appropriate, since we've already
#  used MAKEMOS, but we do it to put DRIZZLE through its paces.
      echo " "
      echo "  The reduced images are combined as well using the drizzling"
      echo "  algorithm."
      echo " "
      drizzle \
         in='data-??' \
         out=drizzled \
         accept
#     end drizzle

#  Display the final mosaic.  We use the drizzled one, but 'mosaic' could
#  have been used instead.
      if ( "$device" != "NONE" ) then 
         echo " "
         echo "  Displaying the final mosaic."
         echo " "
         gdclear \\
         display \
            in=drizzled \
            mode=percentiles \
            percentiles='[2,98]' \
            reset accept
#        end display
      endif

#  Draw outlines of the data frames.
      if ( "$device" != "NONE" ) then
         echo " "
         echo "  Drawing outlines of the images over the displayed mosaic."
         echo " "
         drawndf \
            in='data-1?' \
            noclear \
            nopenrot \
            style='"colour=2"' \
            reset accept
#        end drawndf
         drawndf \
            in='data-2?' \
            noclear \
            nopenrot \
            style='"colour=3"' \
            reset accept
#        end drawndf
      endif

#  Exercise is completed.
      ccdnote \
         '"Exercise completed"' \
	 accept
#     end ccdnote

#  Exit label
EXIT:
      exit
# $Id$
